apply plugin: 'maven'
apply plugin: 'signing'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = "com.scalar-labs"
archivesBaseName = "scalardb"
version = "1.0.0-rc1"

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Scalar DB'
                packaging 'jar'
                // optionally artifactId can be defined here 
                description 'A library that provides a distributed storage abstraction and client-coordinated distributed transaction manager on the storage.'
                url 'https://github.com/scalar-labs/scalardb'

                scm {
                    connection 'scm:git:https://github.com/scalar-labs/scalardb.git'
                    developerConnection 'scm:git:https://github.com/scalar-labs/scalardb.git'
                    url 'https://github.com/scalar-labs/scalardb'
                }

                licenses {
                    license {
                        name 'GNU Affero General Public License, Version 3'
                        url 'http://www.gnu.org/licenses/agpl-3.0.html'
                    }
                }

                developers {
                    developer {
                        id 'hiroyuki'
                        name 'Hiroyuki Yamada'
                        email 'hiroyuki.yamada@scalar-labs.com'
                    }
                }
            }
        }
    }
}
