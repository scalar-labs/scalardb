plugins {
    id 'com.github.johnrengelman.shadow' version "7.1.2"
    id 'application'
}

group = 'com.scalar'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}
application {
    mainClass = 'com.scalar.db.datapopulator.Main'
}

tasks.register('runBulkInsert', JavaExec) {
    group = 'custom'
    description = 'Runs the main class for bulk inserts'
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set('com.scalar.db.datapopulator.Main')

    // Set default arguments or pass via command line
    args = [
            'scalardb.properties', // path to ScalarDB properties
            '10000'                // number of records
    ]
}

dependencies {
    implementation 'com.scalar-labs:scalardb:3.15.2'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

// Build a fat jar
shadowJar {
    archiveClassifier.set("")
    manifest {
        attributes 'Main-Class': 'com.scalar.db.datapopulator.Main'
    }
    mergeServiceFiles()
}