plugins {
    id 'net.ltgt.errorprone' version "${errorpronePluginVersion}"
    id 'com.github.johnrengelman.shadow' version "${shadowPluginVersion}"
    id 'com.github.spotbugs' version "${spotbugsPluginVersion}"
    id 'maven-publish'
}

sourceSets {
    integrationTestAll {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}
configurations {
    integrationTestAllImplementation.extendsFrom testImplementation
    integrationTestAllImplementation.extendsFrom testRuntimeOnly
    integrationTestAllCompileOnly.extendsFrom testCompileOnly
}

task integrationTestAll(type: Test) {
    description = 'Runs integration tests for data-loader CLI'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTestAll.output.classesDirs
    classpath = sourceSets.integrationTestAll.runtimeClasspath
    useJUnitPlatform()
    shouldRunAfter test
    outputs.upToDateWhen { false }  // ensures integration tests are run every time when called
}

base {
    archivesName = "scalardb-data-loader-cli"
}

dependencies {
    implementation project(':core')
    implementation project(':data-loader:data-loader-core')
    implementation "org.slf4j:slf4j-simple:${slf4jVersion}"
    implementation "info.picocli:picocli:${picocliVersion}"

    // for SpotBugs
    compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugsVersion}"
    testCompileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugsVersion}"

    // for Error Prone
    errorprone "com.google.errorprone:error_prone_core:${errorproneVersion}"
    errorproneJavac "com.google.errorprone:javac:${errorproneJavacVersion}"
    testImplementation "org.testcontainers:testcontainers:2.0.2"
    testImplementation "org.testcontainers:testcontainers-mysql:2.0.2"
    testImplementation "com.datastax.cassandra:cassandra-driver-core:3.11.2"
}

javadoc {
    title = "ScalarDB Data Loader CLI"
}

// Build a fat jar
shadowJar {
    archiveClassifier.set("")
    manifest {
        attributes 'Main-Class': 'com.scalar.db.dataloader.cli.DataLoaderCli'
    }
    mergeServiceFiles()
}

spotless {
    java {
        target 'src/*/java/**/*.java'
        importOrder()
        removeUnusedImports()
        googleJavaFormat(googleJavaFormatVersion)
    }
}

spotbugsMain.reports {
    html.enabled = true
}
spotbugsMain.excludeFilter = file("${project.rootDir}/gradle/spotbugs-exclude.xml")

spotbugsTest.reports {
    html.enabled = true
}
spotbugsTest.excludeFilter = file("${project.rootDir}/gradle/spotbugs-exclude.xml")

task dockerfileLint(type: Exec) {
    description 'Lint the Dockerfile'
    commandLine "${project.rootDir}/ci/dockerfile_lint.sh"
}

task copyFilesToDockerBuildContextDir(type: Copy) {
    description 'Copy files to a temporary folder to build the Docker image'
    dependsOn shadowJar
    from("Dockerfile")
    from(tasks.shadowJar.archiveFile)
    into('build/docker')
}

task docker(type: Exec) {
    description 'Build ScalarDB Data Loader Docker CLI image'
    dependsOn copyFilesToDockerBuildContextDir
    workingDir 'build/docker'
    commandLine 'docker', 'build', "--tag=ghcr.io/scalar-labs/scalardb-data-loader-cli:${project.version}", "."
}

// for archiving and uploading to maven central
if (project.gradle.startParameter.taskNames.any { it.endsWith('publish') } ||
        project.gradle.startParameter.taskNames.any { it.endsWith('publishToMavenLocal') }) {
    // not to publish the fat jar to maven central
    shadowJar.enabled = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'scalardb-data-loader-cli'
            from components.java
            pom {
                name = 'ScalarDB Data Loader CLI'
                description = 'CLI application for exporting data from and importing data into ScalarDB'
                url = 'https://github.com/scalar-labs/scalardb'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'hiroyuki'
                        name = 'Hiroyuki Yamada'
                        email = 'hiroyuki.yamada@scalar-labs.com'
                    }
                    developer {
                        id = 'brfrn169'
                        name = 'Toshihiro Suzuki'
                        email = 'brfrn169@gmail.com'
                    }
                    developer {
                        id = 'Torch3333'
                        name = 'Vincent Guilpain'
                        email = 'vincent.guilpain@scalar-labs.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/scalar-labs/scalardb.git'
                    developerConnection = 'scm:git:https://github.com/scalar-labs/scalardb.git'
                    url = 'https://github.com/scalar-labs/scalardb'
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}
