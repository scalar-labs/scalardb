buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:5.14.0'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
//    apply plugin: 'jacoco'
    apply plugin: 'java-library-distribution'
    apply plugin: 'com.diffplug.spotless'

    project.version = '4.0.0-SNAPSHOT'
    group = "com.scalar-labs"

    repositories {
        mavenCentral()
    }

    test {
        maxHeapSize "512m"
        testLogging.showStandardStreams = true
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        options.addStringOption("notimestamp", "com.scalar.db")
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    distZip {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }

    distTar {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }

    installDist {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }
}
