apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'java-library-distribution'

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java/com/scalar/db/storage/cassandra')
            //srcDir file('src/integration-test/java/com/scalar/db/storage/cosmos')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version: '3.6.0'
    compile group: 'com.azure', name: 'azure-cosmos', version: '4.0.1'
    compile group: 'org.jooq', name: 'jooq', version: '3.13.2'
    compile group: 'com.google.guava', name: 'guava', version: '24.1-jre'
    compile group: 'com.google.inject', name: 'guice', version: '4.2.0'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.9.1'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.16.0'
}

test {
    maxHeapSize "512m"
    testLogging.showStandardStreams = true
}

javadoc {
    options.addStringOption("notimestamp", "com.scalar.db")
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task testJar(type: Jar) {
  classifier = 'tests'
  from sourceSets.test.output
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }  // ensures integration tests are run every time when called
    shouldRunAfter test
}

// build should not depend on the integration tests
check.dependsOn -= integrationTest

task copyTestJarsToTestLib(type: Copy) {
    from configurations.testCompile
    into file("$buildDir/test-libs")
}

test.dependsOn += copyTestJarsToTestLib

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "com.scalar-labs"
archivesBaseName = "scalardb"
//version = "2.1.0"

// for archiving and uploading to maven central
//apply from: 'archive.gradle'
