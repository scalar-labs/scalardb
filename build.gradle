plugins {
    id "com.diffplug.spotless" version "6.13.0"
    id "org.jreleaser" version "1.20.0"
}

ext {
    projectGroup = 'com.scalar-labs'
    projectVersion = '3.17.0-SNAPSHOT'
}

group = projectGroup
version = projectVersion

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
//    apply plugin: 'jacoco'
    apply plugin: 'java-library-distribution'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'com.scalar.db.jdk-configuration'

    project.version = projectVersion

    ext {
        guiceVersion = '5.1.0'
        guavaVersion = '32.1.3-jre'
        slf4jVersion = '1.7.36'
        cassandraDriverVersion = '3.11.5'
        azureCosmosVersion = '4.74.0'
        jooqVersion = '3.14.16'
        awssdkVersion = '2.33.0'
        commonsDbcp2Version = '2.13.0'
        mysqlDriverVersion = '8.4.0'
        postgresqlDriverVersion = '42.7.7'
        oracleDriverVersion = '23.9.0.25.07'
        sqlserverDriverVersion = '12.8.1.jre8'
        sqliteDriverVersion = '3.50.3.0'
        yugabyteDriverVersion = '42.7.3-yb-4'
        db2DriverVersion = '12.1.2.0'
        mariadDbDriverVersion = '3.5.5'
        picocliVersion = '4.7.7'
        commonsTextVersion = '1.14.0'
        junitVersion = '5.13.4'
        commonsLangVersion = '3.18.0'
        assertjVersion = '3.27.4'
        mockitoVersion = '4.11.0'
        spotbugsVersion = '4.8.6'
        errorproneVersion = '2.10.0'
        errorproneJavacVersion = '9+181-r4173-1'
        gsonVersion = '2.13.1'
        stefanbirknerSystemLambdaVersion = '1.2.1'
        spotbugsPluginVersion = '5.2.5'
        errorpronePluginVersion = '3.1.0'
        shadowPluginVersion = '7.1.2'
        // Make JDK11+ use the same version as JDK8 uses
        googleJavaFormatVersion = '1.7'
        commonsIoVersion = '2.16.1'
        lombokVersion = '1.18.34'
    }

    repositories {
        mavenCentral()
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        testLogging.showStandardStreams = true
    }

    group = projectGroup

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    distZip {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }

    distTar {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }

    installDist {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }
}

jreleaser {
    gitRootSearch = true

    signing {
        active = 'ALWAYS'
        armored = true
    }

    deploy {
        maven {
            def stagingRepositories = ['core/build/staging-deploy',
                                       'integration-test/build/staging-deploy',
                                       'schema-loader/build/staging-deploy',
                                       'data-loader/core/build/staging-deploy']
            mavenCentral {
                sonatype {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepositories.each { stagingRepository(it) }
                }
            }
            nexus2 {
                'snapshot-deploy' {
                    active = 'SNAPSHOT'
                    snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                    applyMavenCentralRules = true
                    snapshotSupported = true
                    closeRepository = true
                    releaseRepository = true
                    stagingRepositories.each { stagingRepository(it) }
                }
            }
        }
    }
}
