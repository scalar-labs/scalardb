plugins {
    id "com.diffplug.spotless" version "6.13.0"
    id "org.jreleaser" version "1.21.0"
}

ext {
    projectGroup = 'com.scalar-labs'
    projectVersion = '3.18.0-SNAPSHOT'
}

group = projectGroup
version = projectVersion

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
//    apply plugin: 'jacoco'
    apply plugin: 'java-library-distribution'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'com.scalar.db.jdk-configuration'

    project.version = projectVersion

    ext {
        guiceVersion = '5.1.0'
        guavaVersion = '32.1.3-jre'
        slf4jVersion = '1.7.36'
        cassandraDriverVersion = '3.12.1'
        azureCosmosVersion = '4.77.0'
        azureBlobStorageVersion = '12.33.0'
        jooqVersion = '3.14.16'
        awssdkVersion = '2.41.1'
        hikariCpVersion = '4.0.3'
        mysqlDriverVersion = '8.4.0'
        postgresqlDriverVersion = '42.7.9'
        oracleDriverVersion = '23.26.0.0.0'
        sqlserverDriverVersion = '12.8.2.jre8'
        sqliteDriverVersion = '3.51.1.0'
        yugabyteDriverVersion = '42.7.3-yb-4'
        db2DriverVersion = '12.1.3.0'
        mariadDbDriverVersion = '3.5.7'
        alloyDbJdbcConnectorVersion = '1.2.9'
        googleCloudStorageVersion = '2.62.1'
        picocliVersion = '4.7.7'
        commonsTextVersion = '1.15.0'
        caffeineVersion = '2.9.3'
        junitVersion = '5.14.2'
        commonsLangVersion = '3.20.0'
        assertjVersion = '3.27.7'
        mockitoVersion = '4.11.0'
        spotbugsVersion = '4.8.6'
        errorproneVersion = '2.10.0'
        errorproneJavacVersion = '9+181-r4173-1'
        gsonVersion = '2.13.2'
        stefanbirknerSystemLambdaVersion = '1.2.1'
        spotbugsPluginVersion = '5.2.5'
        errorpronePluginVersion = '3.1.0'
        shadowPluginVersion = '7.1.2'
        // Make JDK11+ use the same version as JDK8 uses
        googleJavaFormatVersion = '1.7'
        commonsIoVersion = '2.16.1'
        lombokVersion = '1.18.34'
    }

    repositories {
        mavenCentral()
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        testLogging.showStandardStreams = true
    }

    group = projectGroup

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    distZip {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }

    distTar {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }

    installDist {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }
}

jreleaser {
    gitRootSearch = true

    signing {
        active = 'ALWAYS'
        armored = true
    }

    deploy {
        maven {
            def stagingRepositories = ['core/build/staging-deploy',
                                       'integration-test/build/staging-deploy',
                                       'schema-loader/build/staging-deploy',
                                       'data-loader/core/build/staging-deploy',
                                       'data-loader/cli/build/staging-deploy']
            mavenCentral {
                sonatype {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepositories.each { stagingRepository(it) }
                }
            }
            nexus2 {
                'snapshot-deploy' {
                    active = 'SNAPSHOT'
                    snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                    applyMavenCentralRules = true
                    snapshotSupported = true
                    closeRepository = true
                    releaseRepository = true
                    stagingRepositories.each { stagingRepository(it) }
                }
            }
        }
    }
}
