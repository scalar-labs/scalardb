buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:6.10.0'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
//    apply plugin: 'jacoco'
    apply plugin: 'java-library-distribution'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'com.scalar.db.jdk-configuration'

    project.version = '3.8.7-SNAPSHOT'

    ext {
        guiceVersion = '5.0.1'
        guavaVersion = '32.1.1-jre'
        slf4jVersion = '1.7.30'
        cassandraDriverVersion = '3.6.0'
        azureCosmosVersion = '4.53.1'
        jooqVersion = '3.13.2'
        awssdkVersion = '2.19.16'
        commonsDbcp2Version = '2.8.0'
        mysqlDriverVersion = '8.0.31'
        postgresqlDriverVersion = '42.7.2'
        oracleDriverVersion = '19.8.0.0'
        sqlserverDriverVersion = '8.4.1.jre8'
        grpcVersion = '1.60.0'
        protobufVersion = '3.21.12'
        annotationVersion = '1.3.2'
        picocliVersion = '4.1.4'
        scalarAdminVersion = '1.2.0'
        dropwizardMetricsVersion = '4.2.2'
        prometheusVersion = '0.12.0'
        commonsTextVersion = '1.10.0'
        jettyVersion = '9.4.53.v20231009'
        junitVersion = '5.8.2'
        commonsLangVersion = '3.12.0'
        assertjVersion = '3.22.0'
        mockitoVersion = '3.12.4'
        spotbugsVersion = '4.6.0'
        errorproneVersion = '2.10.0'
        errorproneJavacVersion = '9+181-r4173-1'
        gsonVersion = '2.9.0'
        log4jVersion = '2.17.1'
        stefanbirknerSystemLambdaVersion = '1.2.1'
        spotbugsPluginVersion = '5.2.5'
        errorpronePluginVersion = '3.1.0'
        protobufPluginVersion = '0.8.17'
        shadowPluginVersion = '7.1.2'
        // Make JDK11+ use the same version as JDK8 uses
        googleJavaFormatVersion = '1.7'
    }

    repositories {
        mavenCentral()
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        testLogging.showStandardStreams = true
    }

    group = "com.scalar-labs"

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    distZip {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }

    distTar {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }

    installDist {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }
}

