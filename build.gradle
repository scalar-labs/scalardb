buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:6.13.0'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
//    apply plugin: 'jacoco'
    apply plugin: 'java-library-distribution'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'com.scalar.db.jdk-configuration'

    project.version = '3.16.0-SNAPSHOT'

    ext {
        guiceVersion = '5.1.0'
        guavaVersion = '32.1.3-jre'
        slf4jVersion = '1.7.36'
        cassandraDriverVersion = '3.11.5'
        azureCosmosVersion = '4.68.0'
        jooqVersion = '3.14.16'
        awssdkVersion = '2.31.3'
        commonsDbcp2Version = '2.13.0'
        mysqlDriverVersion = '8.4.0'
        postgresqlDriverVersion = '42.7.5'
        oracleDriverVersion = '23.8.0.25.04'
        sqlserverDriverVersion = '12.8.1.jre8'
        sqliteDriverVersion = '3.49.1.0'
        yugabyteDriverVersion = '42.7.3-yb-4'
        db2DriverVersion= '12.1.0.0'
        mariadDbDriverVersion = '3.5.3'
        picocliVersion = '4.7.7'
        commonsTextVersion = '1.13.1'
        junitVersion = '5.11.4'
        commonsLangVersion = '3.17.0'
        assertjVersion = '3.27.3'
        mockitoVersion = '4.11.0'
        spotbugsVersion = '4.8.6'
        errorproneVersion = '2.10.0'
        errorproneJavacVersion = '9+181-r4173-1'
        gsonVersion = '2.13.1'
        stefanbirknerSystemLambdaVersion = '1.2.1'
        spotbugsPluginVersion = '5.2.5'
        errorpronePluginVersion = '3.1.0'
        shadowPluginVersion = '7.1.2'
        // Make JDK11+ use the same version as JDK8 uses
        googleJavaFormatVersion = '1.7'
        commonsIoVersion = '2.16.1'
        lombokVersion = '1.18.34'
    }

    repositories {
        mavenCentral()
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        testLogging.showStandardStreams = true
    }

    group = "com.scalar-labs"

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    distZip {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }

    distTar {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }

    installDist {
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }
}

