plugins {
    id 'java-gradle-plugin'
    id 'com.diffplug.spotless' version '6.13.0'
}

repositories {
    mavenCentral()
}

gradlePlugin {
    plugins {
        jdkConfiguration {
            id = 'com.scalar.db.jdk-configuration'
            implementationClass = 'com.scalar.db.JdkConfigurationPlugin'
        }
    }
}

spotless {
    java {
        target 'src/*/java/**/*.java'
        importOrder()
        removeUnusedImports()
        // Spotless v6 doesn't have forbidWildcardImports (added in v8). As a workaround, use custom step
        // with the regex from: https://github.com/diffplug/spotless/blob/67f7df71cb29833c2f94789dc31e9ecff18a71c5/lib/src/main/java/com/diffplug/spotless/java/ForbidWildcardImportsStep.java#L31
        custom 'forbidWildcardImports', { source ->
            if (source =~ /(?m)^import\s+(?:static\s+)?[^;\n]*\*;\R?/) {
                throw new AssertionError('Wildcard imports are not allowed')
            }
            source
        }
        googleJavaFormat("1.7")
    }
}
